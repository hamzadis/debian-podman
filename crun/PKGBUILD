# Maintainer: Adis HamziÄ‡ <adis at hamzadis dot com>

pkgbase=crun
pkgname=crun1
pkgver='1.18'
pkgrel='1'
pkgdesc="A fast and lightweight fully featured OCI runtime and C library for running containers"
arch=("amd64")
url="https://github.com/containers/crun"
license=('GPL2')
provides=('crun')
conflicts=("${pkgbase}" "${pkgbase}-git" "${pkgbase}-bin")

makedepends=(
  'git'
  'automake'
  'libtool'
  'pkgconf'
  'python3'
  'gperf'
  'go-md2man'
  'libcap-dev'
  'libseccomp-dev'
  'libsystemd-dev'
  'libyajl-dev'
)
depends=(
  'libc6'
  'libcap2'
  'libseccomp2'
  'libsystemd0'
  'libyajl2'
)

source=(
  "git+${url}#tag=${pkgver}"
)
b2sums=('SKIP')

build() {
  cd "${pkgbase}"
  git submodule update --recursive

  ./autogen.sh
  ./configure --prefix="/usr" --enable-dynamic
  make
}

package() {
  cd "${pkgbase}"
  make DESTDIR="$pkgdir" install
}

